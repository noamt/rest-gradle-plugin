/*
 * Copyright 2013 Noam Y. Tenne
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.3'
    }
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'project-reports'
apply plugin: 'com.gradle.plugin-publish'

group = 'org._10ne.gradle'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile 'org.codehaus.groovy:groovy-all:2.4.4'
    compile('org.codehaus.groovy.modules.http-builder:http-builder:0.7.1') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        exclude group: 'org.codehaus.groovy', module: 'groovy'
    }
    testCompile ('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:3.1'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

configure(install.repositories.mavenInstaller) {
    pom.project {
        name('rest-gradle-plugin')
        description('A Gradle plugin that provides a task infrastructure to perform REST requests.')
        url('https://github.com/noamt/rest-gradle-plugin')
        inceptionYear '2013'
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        scm {
            url "https://github.com/noamt/rest-gradle-plugin.git"
        }
        developers {
            developer {
                id 'noamt'
                name 'Noam Y. Tenne'
                roles {
                    role 'Developer'
                }
            }
        }
    }
}

pluginBundle {
    website = 'http://www.10ne.org/'
    vcsUrl = 'https://github.com/noamt/rest-gradle-plugin.git'
    description = 'A Gradle plugin that provides a task infrastructure to perform REST requests.'
    
    plugins {
        restPlugin {
            id = 'org.tenne.rest'
            displayName = 'REST Gradle Plugin'
            tags = ['REST', 'API']
            version = '0.4.2'
        }
    }

    mavenCoordinates {
        groupId = "org._10ne.gradle"
        artifactId = "rest-gradle-plugin"
    }
}
